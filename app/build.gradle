plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    namespace 'net.zackzhang.code.haze'
    compileSdk 31

    defaultConfig {
        applicationId 'net.zackzhang.code.haze'
        minSdk 26
        targetSdk 31
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
//                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        def sp = getSecretProperties()
        buildConfigField("String", "QWEATHER_PUBLIC_ID", "\"" + sp['qweather.publicId'] + "\"")
        buildConfigField("String", "QWEATHER_KEY", "\"" + sp['qweather.key'] + "\"")
    }

    signingConfigs {
        release {
            def sp = getSecretProperties()
            keyAlias sp['android.signing.keyAlias']
            keyPassword sp['android.signing.keystorePassword']
            storeFile file(sp['android.signing.keystorePath'])
            storePassword sp['android.signing.keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation 'com.airbnb.android:lottie:4.2.0'
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$android_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    kapt "androidx.room:room-compiler:$room_version"
}

static def generateVersionCode() {
    return Math.ceil((new Date().time - 1462636800000) / 86400000) as int
}

static def generateVersionName() {
    return new Date().format('yyyyMMdd')
}

def getSecretProperties() {
    // rootProject 必须放在外面不然是 null
    def sf = rootProject.file('secret.properties')
    return new Properties().with(true) {
        it.load(new FileInputStream(sf))
    }
}